// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Pre-set the x input
    Mux16(a=x, b=false, sel=zx, out=x-pre);
    Not16(in=x-pre, out=not-x-pre);
    Mux16(a=x-pre, b=not-x-pre, sel=nx, out=x-in);

    // Pre-set the y input
    Mux16(a=y, b=false, sel=zy, out=y-pre);
    Not16(in=y-pre, out=not-y-pre);
    Mux16(a=y-pre, b=not-y-pre, sel=ny, out=y-in);

    // Compute + or &
    Add16(a=x-in, b=y-in, out=x-plus-y);
    And16(a=x-in, b=y-in, out=x-and-y);

    Mux16(a=x-and-y, b=x-plus-y, sel=f, out=out-pre);

    // Post-set the output and set ng
    Not16(in=out-pre, out=not-out-pre);
    Mux16(a=out-pre, b=not-out-pre, sel=no, out=out, out[0..7]=out-least, out[8..15]=out-most, out[15]=ng);

    // Set zr
    Or8Way(in=out-least, out=out-least-nzero);
    Or8Way(in=out-most, out=out-most-nzero);
    Or(a=out-least-nzero, b=out-most-nzero, out=nzr);
    Not(in=nzr, out=zr);
}
